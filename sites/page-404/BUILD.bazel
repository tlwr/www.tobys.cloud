load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_cross_binary", "go_library", "go_test")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push")

go_library(
    name = "page-404_lib",
    srcs = ["main.go"],
    embedsrcs = ["templates/404.tmpl"],
    importpath = "github.com/tlwr/www.tobys.cloud/sites/page-404",
    visibility = ["//visibility:private"],
)

go_test(
    name = "page-404_test",
    srcs = [
        "helpers_test.go",
        "main_test.go",
    ],
    data = [":cmd"],
    deps = [
        "@com_github_onsi_ginkgo_v2//:ginkgo",
        "@com_github_onsi_gomega//:gomega",
        "@com_github_onsi_gomega//gexec",
    ],
)

go_binary(
    name = "cmd",
    out = "page-404",
    embed = [":page-404_lib"],
    visibility = ["//visibility:public"],
)

go_cross_binary(
    name = "linux",
    platform = "@io_bazel_rules_go//go/toolchain:linux_amd64",
    target = ":cmd",
)

pkg_tar(
    name = "app_layer",
    srcs = [":linux"],
    package_dir = "/usr/bin",
)

oci_image(
    name = "image",
    base = "@go_alpine_linux",
    entrypoint = ["/usr/bin/page-404"],
    tars = [":app_layer"],
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/tlwr/page-404",
)
